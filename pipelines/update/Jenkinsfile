def name = 'jenkins'
def buildDir = 'build'

properties([
    parameters([
        string(name: 'baseImage', defaultValue: 'openshift/jenkins-2-centos7', description: 'The base image to build from'),
        string(name: 'jenkinsVersion', defaultValue: '2.105', description: 'The version of Jenkins to use'),
    ])
])

node() {
    stage('Clean Checkout') {
        deleteDir()
        checkout scm
    }

    stage('Prepare Build Artefacts') {
        dir(buildDir) {
            sh "curl -sSL -o jenkins.war ${jenkinsUrl()}"
            writeFile(file: 'plugins.txt', text: createPluginsTxt())
            writeFile(file: 'Dockerfile', text: createDockerfile())
        }
    }

    stage('Build Image') {
        openshift.withCluster() {
            namespace = openshift.project()

            if(!openshift.selector('bc', name).exists()) {
                openshift.newBuild("--name=${name}",
                    "--to=${namespace}/${name}:latest",
                    '--binary=true')
            }

            dir(buildDir) {
                build = openshift.startBuild(name,
                    '--from-dir="."',
                    '--wait=true').narrow('build').object()

                imageDigest = build.status.output.to.imageDigest
                buildNumber = "${build.metadata.annotations.get('openshift.io/build.number')}"

                echo "Build #${buildNumber} created ${namespace}/${name}@${imageDigest}"
             }
        }
    }
}

def jenkinsUrl() {
    "https://updates.jenkins-ci.org/download/war/${params.jenkinsVersion}/jenkins.war"
}

def createPluginsTxt() {
'''\
junit:1.24
subversion:2.10.2
git:3.7.0
script-security:1.40
job-dsl:1.67
config-file-provider:2.17
parameterized-trigger:2.35.2
pipeline-input-step:2.8
pipeline-build-step:2.7
workflow-support:2.18
workflow-cps:2.44
workflow-durable-task-step:2.18
openshift-client:1.0.5
'''
}

def createDockerfile() {
"""\
FROM ${params.baseImage}

ENV JENKINS_VERSION=${params.jenkinsVersion}

COPY jenkins.war /usr/lib/jenkins/jenkins.war
COPY plugins.txt /opt/openshift/configuration/plugins.txt
RUN /usr/local/bin/install-plugins.sh /opt/openshift/configuration/plugins.txt
"""
}